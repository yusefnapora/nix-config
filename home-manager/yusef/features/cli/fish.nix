{ config, pkgs, lib, system, inputs, ... }:
let
  inherit (lib.lists) optionals;
  inherit (lib.attrsets) optionalAttrs;
  inherit (pkgs.stdenv) isLinux isDarwin;
  nix-colors-lib = inputs.nix-colors.lib-contrib { inherit pkgs; };

  inherit (nix-colors-lib) shellThemeFromScheme;
in
{

  home.packages = builtins.attrValues {
    inherit (pkgs)
      eza
      starship
      any-nix-shell
      ;
  };

  programs.fish = {
      enable = true;

      shellAliases = {
          ls = "${pkgs.eza}/bin/eza";
          nix-search = "nix-env -qaP";
          http = "${pkgs.xh}/bin/xh";
          cdrr = "cd (git repo-root)";
        }
        // optionalAttrs isDarwin {
          idea = "open -an 'IntelliJ IDEA.app'";
        };

      functions = {
        # get the current nix store path for the given binary
        nix-which = if isDarwin then "readlink (which $argv[1])" else "readlink -e (which $argv[1])";

        # like nix-which, but stripping out the /bin/$program_name bit
        # useful for checking out other files in the same package
        nix-which-dir = "nix-which $argv[1] | sed -e 's/\\/bin\\/.*$//'";

        # shortcut to trick lazy brain into using `nix shell` instead of
        # `nix-shell -p`
        ns = "nix shell nixpkgs#{$argv}";
      };

      interactiveShellInit = ''
        # init starship prompt
        ${pkgs.starship}/bin/starship init fish | source

        # setup any-nix-shell integration
        ${pkgs.any-nix-shell}/bin/any-nix-shell fish --info-right | source
        
        # If we're at a text console, load shell colors using a script
        # generated by the nix-colors module. Terminal emulators (alacritty, etc)
        # have their colors set elsewhere.
        if [ "$TERM" = "linux" ]
          sh ${shellThemeFromScheme { scheme = config.colorScheme; }}
        end
      '';

      plugins = [
        {
          name = "fish-ssh-agent";
          src = pkgs.fetchFromGitHub {
            owner = "danhper";
            repo = "fish-ssh-agent";
            rev = "fd70a2afdd03caf9bf609746bf6b993b9e83be57";
            sha256 = "sha256-e94Sd1GSUAxwLVVo5yR6msq0jZLOn2m+JZJ6mvwQdLs=";
          };
        }
      ];
  };

  programs.broot = {
      enable = true;
      enableFishIntegration = true;
  };
}
